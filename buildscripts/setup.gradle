def renameConfig = [
        templatePackage: "template",
        newPackage     : "domain.yourname.app",
]

task renameAppPackage(type: Copy) {
    description "Renames the template package in the app module."
    group "Template Setup"

    def newPackageAsDirectory = renameConfig.newPackage.replaceAll("\\.", "/")
    def startingDirectory = "${rootDir}/app/src/main/java/${renameConfig.templatePackage}"
    def endingDirectory = "${rootDir}/app/src/main/java/${newPackageAsDirectory}"

    from(startingDirectory)
    into(endingDirectory)

    // Replace import statements
    filter { line ->
        line.replaceAll(
                "package ${renameConfig.templatePackage}",
                "package ${renameConfig.newPackage}"
        )
    }

    doLast {
        delete(startingDirectory)
    }
}
